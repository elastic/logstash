/*
 * Licensed to Elasticsearch B.V. under one or more contributor
 * license agreements. See the NOTICE file distributed with
 * this work for additional information regarding copyright
 * ownership. Elasticsearch B.V. licenses this file to you under
 * the Apache License, Version 2.0 (the "License"); you may
 * not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *	http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied.  See the License for the
 * specific language governing permissions and limitations
 * under the License.
 */

import org.yaml.snakeyaml.Yaml

// fetch version from Logstash's main versions.yml file
def versionMap = (Map) (new Yaml()).load(new File("$projectDir/../../versions.yml").text)

description = """Ingest JSON to Logstash Grok Config Converter"""
version = versionMap['logstash-core']
String jacksonDatabindVersion = versionMap['jackson-databind']

repositories {
  mavenCentral()
}

buildscript {
  repositories {
    mavenCentral()
    gradlePluginPortal()
  }
  dependencies {
    classpath "org.yaml:snakeyaml:${snakeYamlVersion}"
    classpath "gradle.plugin.com.github.jengelman.gradle.plugins:shadow:${shadowGradlePluginVersion}"
  }
}

dependencies {
  implementation 'net.sf.jopt-simple:jopt-simple:4.6'
  implementation "com.fasterxml.jackson.core:jackson-databind:${jacksonDatabindVersion}"
  testImplementation "junit:junit:4.12"
  testImplementation 'commons-io:commons-io:2.13.0'
}

javadoc {
  enabled = true
}

apply plugin: 'com.github.johnrengelman.shadow'

shadowJar {
  archiveBaseName = 'ingest-converter'
  archiveClassifier = null
  archiveVersion = ''
}

assemble.dependsOn shadowJar
