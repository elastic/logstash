/*
 * Licensed to Elasticsearch B.V. under one or more contributor
 * license agreements. See the NOTICE file distributed with
 * this work for additional information regarding copyright
 * ownership. Elasticsearch B.V. licenses this file to you under
 * the Apache License, Version 2.0 (the "License"); you may
 * not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *	http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied.  See the License for the
 * specific language governing permissions and limitations
 * under the License.
 */

def versionMap = gradle.ext.versions

description = """Logstash End to End Benchmarking Utility"""
version = versionMap['logstash-core']
String jacksonVersion = versionMap['jackson']
String jacksonDatabindVersion = versionMap['jackson-databind']

repositories {
  mavenCentral()
}

buildscript {
  repositories {
    mavenCentral()
    gradlePluginPortal()
  }
  dependencies {
    classpath "com.gradleup.shadow:shadow-gradle-plugin:${shadowGradlePluginVersion}"
  }
}

ext {
  jmh = '1.37'
  elasticsearch = '5.6.16'
}

dependencies {
  implementation 'net.sf.jopt-simple:jopt-simple:5.0.4'
  implementation 'org.apache.httpcomponents:httpclient:4.5.14'
  implementation 'org.apache.commons:commons-compress:1.26.1'
  implementation 'org.apache.commons:commons-lang3:3.18.0'
  implementation 'commons-codec:commons-codec:1.17.0' // transitively required by httpclient

  implementation 'commons-io:commons-io:2.16.1'
  implementation "com.fasterxml.jackson.core:jackson-core:${jacksonVersion}"
  api "com.fasterxml.jackson.core:jackson-databind:${jacksonDatabindVersion}"
  implementation "org.elasticsearch.client:elasticsearch-rest-client:${elasticsearch}"
  implementation "org.openjdk.jmh:jmh-core:$jmh"
  testImplementation 'org.wiremock:wiremock-standalone:3.0.4'

  testImplementation "junit:junit:4.13.2"
}

javadoc {
  enabled = false
}

test {
  // We need to force IPV4 usage to make WireMock tests portable between *nix and Windows.
  // For details see: https://github.com/elastic/logstash/pull/8372
  jvmArgs '-Djava.net.preferIPv4Stack=true'
  exclude '**/org/logstash/benchmark/cli/MainTest*'
  exclude '**/org/logstash/benchmark/cli/MainEsStorageTest*'
}

apply plugin: 'com.gradleup.shadow'
apply plugin: 'java'

shadowJar {
  archiveBaseName = 'benchmark-cli'
  archiveClassifier = null
  archiveVersion = ''
}

assemble.dependsOn shadowJar
