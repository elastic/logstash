import org.yaml.snakeyaml.Yaml

apply plugin: 'java'
apply plugin: 'idea'

// fetch version from Logstash's master versions.yml file
def versionMap = (Map) (new Yaml()).load(new File("$projectDir/../versions.yml").text)

description = "Logstash Core Queue JRuby"
group 'org.logstash'
version = versionMap['logstash-core-queue-jruby']

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.yaml:snakeyaml:1.17'
    }
}

repositories {
    mavenCentral()
}

gradle.projectsEvaluated {
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    }
}

project.sourceCompatibility = 1.8
project.targetCompatibility = 1.8

task sourcesJar(type: org.gradle.api.tasks.bundling.Jar, dependsOn: classes) {
    from sourceSets.main.allSource
    classifier 'sources'
    extension 'jar'
}

task javadocJar(type: org.gradle.api.tasks.bundling.Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
    classifier 'javadoc'
    extension 'jar'
}

// copy jar file into the gem lib dir but without the version number in filename
task copyGemjar(type: org.gradle.api.tasks.Copy, dependsOn: sourcesJar) {
    from project.jar
    into project.file('lib/logstash-core-queue-jruby/')
    rename(/(.+)-${project.version}.jar/, '$1.jar')
}

task cleanGemjar {
    delete fileTree(project.file('lib/logstash-core-queue-jruby/')) {
        include '*.jar'
    }
}

clean.dependsOn(cleanGemjar)
jar.finalizedBy(copyGemjar)

task gemspec_jars << {
    File gemspec_jars = file("./gemspec_jars.rb")
    gemspec_jars.newWriter().withWriter { w ->
        w << "# This file is generated by Gradle as part of the build process. It extracts the build.gradle\n"
        w << "# runtime dependencies to generate this gemspec dependencies file to be eval'ed by the gemspec\n"
        w << "# for the jar-dependencies requirements.\n\n"
        configurations.runtime.allDependencies.each { dependency ->
            w << "gem.requirements << \"jar ${dependency.group}:${dependency.name}, ${dependency.version}\"\n"
        }
    }
}
build.finalizedBy(gemspec_jars)

configurations.create('sources')
configurations.create('javadoc')
configurations.archives {
    extendsFrom configurations.sources
    extendsFrom configurations.javadoc
}

artifacts {
    sources(sourcesJar) {
        // Weird Gradle quirk where type will be used for the extension, but only for sources
        type 'jar'
    }

    javadoc(javadocJar) {
        type 'javadoc'
    }
}

configurations {
    provided
}

project.sourceSets {
    main.compileClasspath += project.configurations.provided
    main.runtimeClasspath += project.configurations.provided
    test.compileClasspath += project.configurations.provided
    test.runtimeClasspath += project.configurations.provided
}
project.javadoc.classpath += project.configurations.provided

idea {
    module {
        scopes.PROVIDED.plus += [project.configurations.provided]
    }
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
    provided group: 'org.jruby', name: 'jruby-core', version: '1.7.25'
    provided files('../logstash-core-event-java/lib/logstash-core-event-java/logstash-core-event-java.jar')
    provided files('../logstash-core/lib/logstash-core/logstash-core.jar')
}

// See http://www.gradle.org/docs/current/userguide/gradle_wrapper.html
task wrapper(type: Wrapper) {
    description = 'Install Gradle wrapper'
    gradleVersion = '2.8'
}
